import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.DetektCreateBaselineTask

buildscript {
    repositories {
        google()
    }
    dependencies {
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:2.5.3"
    }
}

// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '8.0.2' apply false
    id 'com.android.library' version '8.0.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.8.0' apply false
    id "io.gitlab.arturbosch.detekt" version "1.22.0"
}

def projectSource = file(projectDir)
def configFile = files("$rootDir/detekt-config.yml")
def baselineFile = file("$rootDir/detekt-baseline.xml")
def kotlinFiles = "**/*.kt"
def ktsFiles = "**/*.kts"
def resourceFiles = "**/resources/**"
def buildFiles = "**/build/**"

apply plugin: 'io.gitlab.arturbosch.detekt'

tasks.register("detektFormat", Detekt) {
    description = "Custom DETEKT build for all modules"
    parallel = true
    ignoreFailures = false
    autoCorrect = true
    buildUponDefaultConfig = true
    setSource(projectSource)
    baseline.set(baselineFile)
    config.setFrom(configFile)
    include(kotlinFiles, ktsFiles)
    exclude(resourceFiles, buildFiles)
    reports {
        html.enabled = true
        xml.enabled = false
        txt.enabled = false
    }
}

tasks.register("detektCheck", Detekt) {
    description = "Custom DETEKT build for all modules"
    parallel = true
    ignoreFailures = false
    autoCorrect = false
    buildUponDefaultConfig = true
    setSource(projectSource)
    baseline.set(baselineFile)
    config.setFrom(configFile)
    include(kotlinFiles, ktsFiles)
    exclude(resourceFiles, buildFiles)
    reports {
        html.enabled = true
        xml.enabled = false
        txt.enabled = false
    }
}

tasks.register("detektIgnore", DetektCreateBaselineTask) {
    description = "Overrides current baseline"
    parallel = true
    ignoreFailures = true
    buildUponDefaultConfig = true
    setSource(files(rootDir))
    baseline.set(baselineFile)
    config.setFrom(configFile)
    include(kotlinFiles, ktsFiles)
    exclude(resourceFiles, buildFiles)
}

dependencies {
    detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:1.22.0"
}

allprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}
